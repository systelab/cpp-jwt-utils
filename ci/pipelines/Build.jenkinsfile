def channel = "testing"
def version = "0.0.0"
def profile = "vs2022.conanprofile"
def archs = ['x86', 'x86_64']
def configs = ['Debug', 'Release']

library identifier: "cpp-jenkins-pipelines@master", retriever: modernSCM(
  [$class: "GitSCMSource",
   remote: "https://github.com/systelab/cpp-jenkins-pipelines.git"])
   
pipeline
{
	agent
	{
		label 'lib-build'
	}

	parameters
	{
		string(	name: 'version',
				description: 'Version to build (must match the name of the tag that will be checked out), leave blank for checkout of current branch',
				defaultValue: '' )

		booleanParam( name: 'stable',
					  description: 'Show if generated library should be uploaded as stable or testing',
					  defaultValue: false )
		
		booleanParam( name: 'uploadPackage',
					  description: 'Whether or not to upload testing conan package',
					  defaultValue: false )
	}

	options
	{
		skipDefaultCheckout(true)
		disableConcurrentBuilds()
		buildDiscarder(logRotator(numToKeepStr: '5'))
	}

	stages
	{
		stage('Checkout')
		{
			steps
			{
				deleteDir()
				script 
				{
					if (params.version == '')
					{
						checkoutSourceCode()
					}
					else
					{
						checkoutSourceCodeFromTag(version)
						version = params.version
					}
					channel = params.stable ? "stable" : "testing"
				}
			}
		}
		
		stage('Build')
		{	
			steps
			{
				script
				{
					archs.each
					{ arch ->
						configs.each
						{ config ->
							stage("Build ${config}|${arch}")
							{
								def buildFolder = "build/${config}-${arch}"
								bat "conan install . --install-folder ${buildFolder} --profile=${profile} -s arch=${arch} -s build_type=${config}"
								bat "conan build . --build-folder ${buildFolder}" 
								bat "conan export-pkg . JWTUtils/${version}@systelab/${channel} --build-folder ${buildFolder} --force"
								bat "conan test ./test_package/conanfile.py JWTUtils/${version}@systelab/${channel} --profile=${profile} -s arch=${arch} -s build_type=${config}"
								dir("${buildFolder}/bin/${config}/")
								{
									bat "JWTUtilsTest.exe --gtest_output=xml:JWTUtilsTest.xml"
								}
								dir("test/JWTUtilsTestUtilities")
								{
									bat "conan install . --install-folder ${buildFolder} --profile=../../${profile} -s arch=${arch} -s build_type=${config} -e VERSION=${version} -e CHANNEL=${channel}"
									bat "conan build . --build-folder ${buildFolder}" 
									bat "conan export-pkg . JWTUtilsTestUtilities/${version}@systelab/${channel} --build-folder ${buildFolder} --force"
									bat "conan test ./test_package/conanfile.py JWTUtilsTestUtilities/${version}@systelab/${channel} --profile=../../${profile} -s arch=${arch} -s build_type=${config}"
								}
							}
						}
					}
				}
			}
		}

		stage('Deploy')
		{
			when
			{
				expression { params.uploadPackage }
			}
			steps
			{
				bat "conan upload JWTUtils/${version}@systelab/${channel} --all -r systelab-conan-local --force"
				bat "conan upload JWTUtilsTestUtilities/${version}@systelab/${channel} --all -r systelab-conan-local --force"
			}
		}	
	}
	
	post
	{
		always
		{
			junit allowEmptyResults: true, testResults: "build*/JWTUtilsTest.xml"
			script
			{
				currentBuild.description = "${version}/${channel}"
			}
		}
	}
}